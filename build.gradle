defaultTasks 'clean', 'build'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

description = 'JSON/XML/YAML Test'
group = 'ch.frostnova.test'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def dateTimeFormat = java.time.format.DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss")
def buildDate = java.time.LocalDateTime.now()

jar {
    manifest {
        attributes(
                "Name": project.name,
                "Version": project.version,
                "Build-Date": dateTimeFormat.format(buildDate)
        )
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}



ext.libs = [
        json              : [
                "com.fasterxml.jackson.core:jackson-annotations:2.9.6",
                "com.fasterxml.jackson.core:jackson-databind:2.9.6",
                "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.6"
        ],
        xml : [
                "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.6"
        ],
        yaml : [
                "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.6"
        ],
        csv: [
                "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.8.8"
        ],
        testbase          : [
                "junit:junit:4.12",
                "org.mockito:mockito-core:2.18.3"
        ],
        loggingAPI   : [
                "org.slf4j:slf4j-api:1.7.25"
        ],
        loggingImpl  : [
                "org.slf4j:jcl-over-slf4j:1.7.25",
                "org.slf4j:jul-to-slf4j:1.7.25",
                "ch.qos.logback:logback-classic:1.2.3"
        ]
]

dependencies {
    compile libs.json
    compile libs.xml
    compile libs.yaml
    compile libs.csv
    compile libs.loggingAPI
    compile libs.loggingImpl

    testCompile libs.testbase
}
